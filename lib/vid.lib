#importonce
.filenamespace vid

.cpu _65c02
.encoding "ascii"

#if !MACROS_ONLY

#import "via.lib"
#import "zp.lib"
#import "macros.lib"

// Video addresses
#if !PCB
    .label VRAM_START = $7000
    .label VRAM_END = $7fff
    .label VRAM_TXT_END = $71ff
#else 
    .label VRAM_START = $6000
    .label VRAM_END = $77ff
    .label VRAM_TXT_END = $61ff
    .label MODE_REG = via.PORTC
    .label MODE_REG_DIR = via.DDC

    .label MODE_TEXT = %00000000
    .label MODE_SG6 = %00100000
    .label MODE_CG1 = %00000001
    .label MODE_RG1 = %00000011
    .label MODE_CG2 = %00000101
    .label MODE_RG2 = %00000111
    .label MODE_CG3 = %00001001
    .label MODE_RG3 = %00001011
    .label MODE_CG6 = %00001101
    .label MODE_RG6 = %00001111


#endif



// Characters
.label CURSOR_ON = %10001111


/* --------------------------------------------------------------------------------------- 
Initialize the display
*/
init:
#if PCB
    lda #$ff
    sta MODE_REG_DIR
#endif

	jsr blank_screen
	set_vid_ptr(0, 0)
	stz zp.cursor_blink_count
    stz zp.vid_flags
	rts


/* --------------------------------------------------------------------------------------- 
Initialize the blinking cursor
*/
init_cursor:
	lda via.IER
	ora #(via.INT_ENABLE | via.TIMER1_INT)
	sta via.IER 									// enable timer 1 interrupts

	lda #via.T1_CONTINUOUS
    sta via.ACR                         			// set up timer 1 for continuous interrupts for cursor blink

    lda #$ff
    sta via.T1_LO                       			// write all 1s to timer 1 lo
    sta via.T1_HI                       			// write all 1s to timer 1 hi, starting the timer

    stz zp.cursor_blink_count						// initialize cursor blink counter

	rts


/* --------------------------------------------------------------------------------------- 
Blank the screen in text mode
*/
blank_screen:
	pha
	phx

	ldx #0
	lda #' '
fill_1st_half:
    sta VRAM_START,x
    inx
    bne fill_1st_half                             	// first loop: 256 times
fill_2nd_half:
    sta VRAM_START+$0100,x
    inx
    bne fill_2nd_half                               // second loop: 256 times

	plx
	pla
	rts


/* --------------------------------------------------------------------------------------- 
write the ascii char in A to the address in the video pointer
writes upper case as inverted, assuming data bit 6 in text mode is INV
*/
write_ascii:
    pha
    phy

    cmp #0                                          // load N and Z flags with A7 and A==0
    bmi video_write                                 // if leading bit is 1, this is a graphic char. write it.
    bne write_ascii_symbol                          
    lda #' '                                        // if A==0, write space
    jmp video_write

write_ascii_symbol:
    cmp #$20                                        // check if code is less than $20
    bcs not_inv_symb                          
    ora #%01100000                                  // if it is, it ascii control char. map to inverted symbols
    jmp video_write
not_inv_symb:

    cmp #$60                                        // check if code is $60 or more
    bcc video_write                                 // if it's below $60, we can immediately write it
keep_bottom_5_bits:
    and #%00011111                                  // if code is < $20 or >=$60, only keep bottom 5 bits

video_write:
    ldy #0
    sta (zp.vid_ptr),y                             	// write six bit ascii to memory at address in VIDEO_PTR

    ply
    pla
    rts


/* --------------------------------------------------------------------------------------- 
write the null-terminated ASCII string at address in zp.B, zp.C 
to the address in the vid pointer. Increments vid_ptr
*/
write_string: {
    phy
    pha
    ldy #0
loop:   
    lda (zp.B),y
    beq done  
    jsr vid.write_ascii
    inc_vid_ptr()
    iny
    jmp loop

done:
    pla
    ply

    rts
}


/* --------------------------------------------------------------------------------------- 
write the number in A to the address in the video pointer
also increment vid ptr twice
*/
write_hex:                                        
    pha                                             // stash number on stack
    pha

    ror
    ror
    ror
    ror
    and #%00001111                                  // pick out high nibble

    cmp #$0a
    bcc hi_nibble_num                               // branch if high nibble is number

    clc
    adc #('a'-10)                                   // set A to ascii letter 
    jmp write_hi_nibble

hi_nibble_num:
    clc
    adc #'0'                                        // set A to ascii number

write_hi_nibble:
    jsr write_ascii                                 // write the high nibble of number
    inc_vid_ptr()

    pla                                             // retrieve number from stack
    and #%00001111                                  // pick out low nibble

    cmp #$0a
    bcc lo_nibble_num                               // branch if low nibble is number

    clc
    adc #('a'-10)                                   // set A to ascii letter
    jmp write_lo_nibble

lo_nibble_num:
    clc
    adc #'0'                                        // set A to ascii number

write_lo_nibble:
    jsr write_ascii                                 // write the low nibble of number
    inc_vid_ptr()

    pla
    rts



/* --------------------------------------------------------------------------------------- 
increment the video and pointer, wrapping from 512 to 0
*/
inc_vid_ptr:
    inc_vid_ptr()
    rts

/* --------------------------------------------------------------------------------------- 
decrement the video pointer, wrapping from 0 to 512
*/
dec_vid_ptr:
    dec_vid_ptr()
    rts


/* --------------------------------------------------------------------------------------- 
Shift every row up 1
*/
shift_up_1:
    shift_vid_up(1, 0)
    rts


/* --------------------------------------------------------------------------------------- 
Shift every row up 1 and clear the bottom row
*/
shift_up_1_clear_bottom:
    jsr shift_up_1
    fill_vid_row(15, ' ')
    set_vid_ptr(15, 0)
    rts

/* --------------------------------------------------------------------------------------- 
The the vid pointer to the beginning of row in Y (0...15)
*/
set_row:
    lda vram_row_starts.lo,y
    sta zp.vid_ptr
    lda vram_row_starts.hi,y
    sta zp.vid_ptr+1   
    rts 
    
vram_row_starts:
    .lohifill 16,vid.VRAM_START+i*32

#endif



/* --------------------------------------------------------------------------------------- 
Handle the cursor timer irq. Increments cursor blink count
*/
.macro @handle_cursor_timer_irq() {
    ldx via.T1_LO                                   // clear timer 1 interrupt by reading lo t1 byte
    inc zp.cursor_blink_count                       // increment the blink counter          
}


/* --------------------------------------------------------------------------------------- 
Write the "on" character if the cursor should be on and the "off" character otherwise.
Only writes if the cursor is in the wrong state, based on bit 0 of zp.vid_flags
*/
.pseudocommand @blink_cursor on : off {
    bbr3 zp.cursor_blink_count,cursor_off           // if bit 3 of blink count is 0, turn off cursor
    bbs0 zp.vid_flags,done                          // if cursor is on, skip writing
    smb0 zp.vid_flags                               // store cursor on
    lda on                                          // otherwise, load cursor char
    jsr vid.write_ascii
    jmp done
cursor_off:
    bbr0 zp.vid_flags,done                          // if cursor is off, skip writing
    rmb0 zp.vid_flags                               // store cursor off
    lda off                                         // load character under cursor
    jsr vid.write_ascii
done:
}


/* --------------------------------------------------------------------------------------- 
Force the cursor on if it should be on (useful when the cursor moves in the on state)
*/
.macro @force_cursor_update() {
    bbr3 zp.cursor_blink_count,done                 // if bit 3 of blink count is 0, cursor is off
    smb0 zp.vid_flags                               // store cursor on
    lda #vid.CURSOR_ON                              // otherwise, load cursor char
    jsr vid.write_ascii
done:
}



/* --------------------------------------------------------------------------------------- 
write the string to the address in the video pointer
writes upper case as inverted, assuming data bit 6 in text mode is INV.
Also increments vid pointer to after the string
*/
.macro @vid_write_string(string) {
    .if (string.size() == 1) {
        lda #string.charAt(0)
        jsr vid.write_ascii
        inc_vid_ptr()
    } else {
        phx
        ldx #0
loop:   lda data,x
        beq done  
        jsr vid.write_ascii
        inc_vid_ptr()
        inx
        jmp loop
data:   .text string
        .byte $00
done:   plx
    }          
}


/* --------------------------------------------------------------------------------------- 
increment the video and pointer, wrapping from 512 to 0
*/


.macro @inc_vid_ptr() {
    inc zp.vid_ptr                                  // increment the video pointer
    bne exit_inc_ptr                                // check if it became 0 when incremented. If not, we're good to return
    inc zp.vid_ptr+1                                // also carry to hi byte of video ptr
    bbr1 zp.vid_ptr+1,exit_inc_ptr                  // if 2s bit of hi byte is 0, we're good to return
    rmb1 zp.vid_ptr+1                               // otherwise, we're passing 512 chars and need to reset to 0
exit_inc_ptr:       
}


/* --------------------------------------------------------------------------------------- 
decrement the video pointer, wrapping from 0 to 512
*/

.macro @dec_vid_ptr() {
    pha
    dec zp.vid_ptr                                  // decrement the video pointer
    lda #$ff
    cmp zp.vid_ptr                                  // check if we carried
    bne exit_dec_video_ptr                          // if not, good to return
    dec zp.vid_ptr+1                                // if we did carry, decrement hi byte
    lda zp.vid_ptr+1                                // load the video pointer hi byte
    cmp #>vid.VRAM_START                            // compare with smallest valid hi byte for VRAM
    bcs exit_dec_video_ptr                          // if video pointer hi byte is >=, can return
    lda #>vid.VRAM_TXT_END                          // otherwise, reset it to end of screen
    sta zp.vid_ptr+1
exit_dec_video_ptr:
    pla
}


/* --------------------------------------------------------------------------------------- 
Shift everything on the display up by n rows, skipping the bottom skip rows.
Moves the video pointer, and uses zp.B, zp.C as stash
*/
.macro @shift_vid_up(n, skip) {
    set_vid_ptr(0, 0)
    lda #<vid.VRAM_START + (mod(n, 8) * 32)
    sta zp.B
    lda #>vid.VRAM_START + floor(n / 8) 
    sta zp.B+1

    ldx #(16 - n - skip)
shift_row_loop:
    ldy #31

shift_col_loop:
    lda (zp.B),y
    sta (zp.vid_ptr),y                             // copy row into previous row
    dey
    bpl shift_col_loop

    lda zp.B
    clc
    adc #32                                        // add 32 to stash ptr
    sta zp.B
    bcc no_inc_stash_hi
    inc zp.B+1

no_inc_stash_hi:
    lda zp.vid_ptr
    clc
    adc #32                                        // add 32 to vid pointer
    sta zp.vid_ptr
    bcc no_inc_vid_hi
    inc zp.vid_ptr+1

no_inc_vid_hi:
    dex
    bne shift_row_loop
    
}


/* --------------------------------------------------------------------------------------- 
Set the video pointer to a specific row and col in text mode.
Row should be in [0, 15]
Col should be in [0, 31] 
*/
.macro @set_vid_ptr(row, col) {
    lda #((<vid.VRAM_START) + (mod(row, 8) * 32 + col))   // load the low byte of the new video pointer
    sta zp.vid_ptr
    lda #((>vid.VRAM_START) + floor(row / 8))             // load the hi byte of the new video pointer
    sta zp.vid_ptr + 1
}


/* --------------------------------------------------------------------------------------- 
Fill a row with a character
*/
.macro @fill_vid_screen(char) {
    pha
    phx

    ldx #0
    lda #char
fill_1st_half:
    sta vid.VRAM_START,x
    inx
    bne fill_1st_half                               // first loop: 256 times
fill_2nd_half:
    sta vid.VRAM_START+$0100,x
    inx
    bne fill_2nd_half                               // second loop: 256 times

    plx
    pla
}


/* --------------------------------------------------------------------------------------- 
Fill a row with a character
*/
.macro @fill_vid_row(row, char) {
    pha
    phy
    set_vid_ptr(row, 0)
    lda #char
    ldy #32
loop:
    jsr vid.write_ascii
    inc zp.vid_ptr
    dey
    bne loop

    ply
    pla
}


#if PCB
.macro @set_vid_mode_text() {
    pha
    lda #MODE_TEXT
    sta MODE_REG
    pla
}

.macro @set_vid_mode_sg6() {
    pha
    lda #MODE_SG6
    sta MODE_REG
    pla
}

.macro @set_vid_mode_cg1() {
    pha
    lda #MODE_CG1
    sta MODE_REG
    pla
}

.macro @set_vid_mode_rg1() {
    pha
    lda #MODE_RG1
    sta MODE_REG
    pla
}


.macro @set_vid_mode_cg2() {
    pha
    lda #MODE_CG2
    sta MODE_REG
    pla
}


.macro @set_vid_mode_rg2() {
    pha
    lda #MODE_RG2
    sta MODE_REG
    pla
}



.macro @set_vid_mode_cg3() {
    pha
    lda #MODE_CG3
    sta MODE_REG
    pla
}


.macro @set_vid_mode_rg3() {
    pha
    lda #MODE_RG3
    sta MODE_REG
    pla
}



.macro @set_vid_mode_cg6() {
    pha
    lda #MODE_CG6
    sta MODE_REG
    pla
}

.macro @set_vid_mode_rg6() {
    pha
    lda #MODE_RG6
    sta MODE_REG
    pla
}


#endif
